buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.standardout:gradle-eclipseconfig:1.1.0'
  }
}

defaultTasks 'clean', 'install'

def srcProjects() {
  subprojects.findAll { new File(it.projectDir, "src").exists() }
}

def javaProjects() {
  subprojects.findAll { new File(it.projectDir, "src/main/java").exists() }
}

def groovyProjects() {
  subprojects.findAll { new File(it.projectDir, "src/main/groovy").exists() }
}

configure(srcProjects()) { project ->
  // common settings

  apply plugin: 'maven-publish'
  apply plugin: 'org.standardout.eclipseconfig'

  group = 'to.wetransform.hale-codegen'
  version = '0.3.0-SNAPSHOT'

  eclipseconfig {
    codeTemplates = rootProject.file('codetemplates.xml')
  }

  repositories {
    maven {
      url 'https://repo.osgeo.org/repository/release/'
    }

    jcenter()

    // haleÂ»studio artifacts
    maven {
      url 'https://artifactory.wetransform.to/artifactory/libs-release'
    }
  }

  configurations.all {
    // ensure SNAPSHOTs are updated every time if needed
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

  project.ext {
    haleVersion = '5.0.1'
    haleResourcesVersion = '2022.8.16'
  }

}

configure(javaProjects()) {
  // common settings for Java projects
  apply plugin: 'java'

  // package javadoc into a jar file
  task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
  }
}

configure(groovyProjects()) {
  // common settings for Groovy projects
  apply plugin: 'groovy'

  // package groovydoc into a jar file
  task packageJavadoc(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
  }
}

configure(srcProjects()) { project ->
  // further common configuration (needs java/groovy config)

  // package source into a jar file
  task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
  }

  // define artifacts for upload
  artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
  }

  publishing {
    repositories {
      maven {

        // upload to wetransform artifactory
        def releasesRepoUrl = 'https://artifactory.wetransform.to/artifactory/libs-release-local'
        def snapshotsRepoUrl = 'https://artifactory.wetransform.to/artifactory/libs-snapshot-local'
        url project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
        credentials {
          username = project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
          password = project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
        }

      }
    }
  }
}

wrapper {
  gradleVersion = '7.6.1'
}
